apply plugin: 'java-library'

group = 'org.pyboof'
version = '0.40'

repositories {
    mavenCentral()
    mavenLocal()

    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    api group: 'org.boofcv', name: 'boofcv-core', version: project.version
    api group: 'org.boofcv', name: 'boofcv-swing', version: project.version
    api 'net.sf.py4j:py4j:0.10.9.2'

    // needed to use Java 11+ syntax with Java 11 byte code
    annotationProcessor('com.github.bsideup.jabel:jabel-javac-plugin:0.4.2')

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

java { toolchain { languageVersion = JavaLanguageVersion.of(17) } }

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    sourceCompatibility = 17
    options.release = 11
}

// Create a jar which contains all the application and all dependencies
task allJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'PyBoof',
                    'Implementation-Version': version,
                    'Main-Class': 'pyboof.PyBoofEntryPoint'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveName = 'PyBoof-all.jar'
    destinationDir = file("${projectDir}/pyboof")

    outputs.upToDateWhen { false }

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from { configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

test {
    ignoreFailures true
    reports.html.enabled = false
}

javadoc {
    configure(options) {
        failOnError = false
    }
}

sourceSets {
    main { java { srcDir 'java/src' } }
    test { java { srcDir 'java/test' }  }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '7.3.3'
}