apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'org.pyboof'
version = '0.36'

repositories {
    mavenCentral()
    mavenLocal()

    maven {
       url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile group: 'org.boofcv', name: 'boofcv-core', version: project.version
    compile group: 'org.boofcv', name: 'boofcv-swing', version: project.version
    compile 'net.sf.py4j:py4j:0.10.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Create a jar which contains all the application and all dependencies
task allJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'PyBoof',
                    'Implementation-Version': version,
                    'Main-Class': 'pyboof.PyBoofEntryPoint'
    }
    archiveName = 'PyBoof-all.jar'
    destinationDir = file("${projectDir}/pyboof")

    outputs.upToDateWhen { false }

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from { configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

test {
    ignoreFailures true
    reports.html.enabled = false
}

javadoc {
    configure(options) {
        failOnError = false
    }
}

sourceSets {
    main {
        java {
            srcDir 'java/src'
        }
    }

    test {
        java {
            srcDir 'java/test'
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '6.4.1'
}